apply plugin: 'com.android.application'

// Load environment-specific .env file based on NODE_ENV
def nodeEnv = System.getenv('NODE_ENV') ?: 'production'
def envSpecificFile = nodeEnv == 'development' ? 
    project.rootProject.file('../.env.development') : 
    project.rootProject.file('../.env.production')
def envProps = new Properties()
if (envSpecificFile.exists()) {
    envProps.load(new FileInputStream(envSpecificFile))
}

// Set app ID based on environment
def appId = envProps.getProperty('PUBLIC_APP_ID', 'doenit.app')
def appName = envProps.getProperty('PUBLIC_APP_NAME', 'Doenit')

// Copy the appropriate google-services file based on environment
def googleServicesSource = nodeEnv == 'development' ? 
    'google-services.dev.json' : 
    'google-services.prod.json'
def googleServicesSourceFile = file(googleServicesSource)
def googleServicesDestFile = file('google-services.json')

if (googleServicesSourceFile.exists()) {
    copy {
        from googleServicesSourceFile
        into projectDir
        rename { 'google-services.json' }
    }
    logger.lifecycle("Copied ${googleServicesSource} to google-services.json for ${nodeEnv} build")
} else {
    logger.warn("Warning: ${googleServicesSource} not found!")
}

apply plugin: 'com.google.gms.google-services'

android {
    namespace "doenit.app"  // Keep static namespace
    compileSdkVersion rootProject.ext.compileSdkVersion
    defaultConfig {
        applicationId appId
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 186
        versionName "1.4.5"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        
        // Set app name based on environment
        manifestPlaceholders = [appName: appName]
        
        aaptOptions {
             // Files and dirs to omit from the packaged assets dir, modified to accommodate modern web apps.
             // Default: https://android.googlesource.com/platform/frameworks/base/+/282e181b58cf72b6ca770dc7ca5f91f135444502/tools/aapt/AaptAssets.cpp#61
            ignoreAssetsPattern '!.svn:!.git:!.ds_store:!*.scc:.*:!CVS:!thumbs.db:!picasa.ini:!*~'
        }
    }
    buildTypes {
        debug {
            // Debug builds will use the app ID as-is (doenit.app.dev for dev, doenit.app for prod)
            versionNameSuffix '-debug'
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

repositories {
    flatDir{
        dirs '../capacitor-cordova-android-plugins/src/main/libs', 'libs'
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "androidx.appcompat:appcompat:$androidxAppCompatVersion"
    implementation "androidx.coordinatorlayout:coordinatorlayout:$androidxCoordinatorLayoutVersion"
    implementation "androidx.core:core-splashscreen:$coreSplashScreenVersion"
    implementation project(':capacitor-android')
    
    // Google Play Billing Library
    def billing_version = "6.2.1"
    implementation "com.android.billingclient:billing:$billing_version"

    implementation platform('com.google.firebase:firebase-bom:33.1.1')
    implementation 'com.google.firebase:firebase-analytics'
    implementation 'com.google.firebase:firebase-messaging'
    implementation 'com.google.firebase:firebase-auth'
    implementation 'com.google.android.gms:play-services-auth:21.2.0'

    testImplementation "junit:junit:$junitVersion"
    androidTestImplementation "androidx.test.ext:junit:$androidxJunitVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$androidxEspressoCoreVersion"
    implementation project(':capacitor-cordova-android-plugins')

}

configurations.all {
    exclude group: 'com.google.firebase', module: 'firebase-analytics-ktx'
}

apply from: 'capacitor.build.gradle'